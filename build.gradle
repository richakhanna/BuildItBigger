// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:2.2.2'

        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}

allprojects {
    repositories {
        jcenter()
    }
}

task runAppEngine(dependsOn: ":backend:appengineRun") {
    evaluationDependsOn ":backend"

    // To launch app engine in daemon mode, so it allows the gradle process to continue
    project(":backend") {
        appengine {
            daemon true
        }
    }

    doLast {
        println "Google app engine started"
    }
}

task stopAppEngine(dependsOn: ":backend:appengineStop") {
    doLast {
        println "Google app engine stopped"
    }
}

task runAllTests(dependsOn: ":app:connectedAndroidTest") {
    mustRunAfter "runAppEngine"
    finalizedBy "stopAppEngine"
    doLast {
        println "All tests completed"
    }
}

task configureTestTask(dependsOn: ["runAppEngine", "runAllTests", "stopAppEngine"]) {
    doLast {
        println "Tests tasks configured & executed"
    }
}

